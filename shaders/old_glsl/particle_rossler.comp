#version 450

#extension GL_EXT_buffer_reference : require

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

layout(buffer_reference, std430) buffer PositionBuffer{ 
	vec3 pos[];
};
layout(buffer_reference, std430) buffer VelocityBuffer{ 
	vec3 vel[];
};

layout( push_constant ) uniform constants
{	
	PositionBuffer positionBufferA;
	PositionBuffer positionBufferB;
    VelocityBuffer velocityBuffer;
    float deltaTime;
    float timeScale;
    uint particleCount;
} pcs;

//inital vel 0,0,0
void main()
{
    uint index = gl_GlobalInvocationID.x;
    if (index >= pcs.particleCount){
        return;
    }
    vec3 pos = pcs.positionBufferA.pos[index];

    float alpha = 0.2;
    float beta = 0.2;
    float sigma = 5.7;
    
    float x_dot = -pos.y - pos.z;
    float y_dot = pos.x + alpha * pos.y;
    float z_dot = beta + (pos.z * (pos.x - sigma));
    vec3 vel = vec3(x_dot, y_dot, z_dot);
    
    pos += vel * pcs.deltaTime * pcs.timeScale;

    pcs.positionBufferB.pos[index] = pos;
    pcs.velocityBuffer.vel[index] = vel;
}